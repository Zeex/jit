cmake_minimum_required(VERSION 2.8)

project(sampjit)

set(SAMPJIT_VERSION_MAJOR  0)
set(SAMPJIT_VERSION_MINOR  1)
set(SAMPJIT_VERSION_PATCH  1)
set(SAMPJIT_VERSION_STRING "0.1.1")

include(CheckIncludeFile)

check_include_file(alloca.h HAVE_ALLOCA_H)
if(HAVE_ALLOCA_H)
	add_definitions(-DHAVE_ALLOCA_H)
endif()
check_include_file(inttypes.h HAVE_INTTYPES_H)
if(HAVE_INTTYPES_H)
	add_definitions(-DHAVE_INTTYPES_H)
endif()
check_include_file(malloc.h HAVE_MALLOC_H)
if(HAVE_MALLOC_H)
	add_definitions(-DHAVE_MALLOC_H)
endif()
check_include_file(stdint.h HAVE_STDINT_H)
if(HAVE_STDINT_H)
	add_definitions(-DHAVE_STDINT_H)
endif()

add_library(jit MODULE
	amx/amx.h
	amx/getch.h
	amx/sclinux.h
	jitasm/jitasm.h
	plugin.cpp
	plugin.h
	plugin.rc
	plugincommon.h
	jit.cpp
	jit.h
)

set_target_properties(jit PROPERTIES PREFIX "")

if(UNIX)
	add_definitions(-DLINUX)
else()
	set(def_path "${sampjit_SOURCE_DIR}/plugin.def")
	if(MINGW)
		set_target_properties(jit PROPERTIES
			LINK_FLAGS "-Wl,--kill-at --def ${def_path}")
	elseif(MSVC)
		set_target_properties(jit PROPERTIES
			LINK_FLAGS "/DEF:${def_path}")
	endif()
endif()

if(CMAKE_COMPILER_IS_GNUCXX)
	set_target_properties(jit PROPERTIES
		COMPILE_FLAGS "-fno-operator-names")
endif()

file(WRITE "pluginversion.h" 
"/* This file is generated by CMake */
#define PLUGIN_VERSION_MAJOR  ${SAMPJIT_VERSION_MAJOR}
#define PLUGIN_VERSION_MINOR  ${SAMPJIT_VERSION_MINOR}
#define PLUGIN_VERSION_PATCH  ${SAMPJIT_VERSION_PATCH}
#define PLUGIN_VERSION_STRING \"${SAMPJIT_VERSION_STRING}\"
")

install(TARGETS jit LIBRARY DESTINATION ".")
if(MSVC)
	set(PDB_NAME "jit.pdb")
	set(PDB_PATH "${CMAKE_CURRENT_BINARY_DIR}/\${CMAKE_INSTALL_CONFIG_NAME}/${PDB_NAME}")
	install(FILES ${PDB_PATH} DESTINATION ".")
endif()

set(CPACK_PACKAGE_NAME ${CMAKE_PROJECT_NAME})
if(WIN32)
	set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${SAMPJIT_VERSION_STRING}-win32")
	set(CPACK_GENERATOR ZIP)
elseif(UNIX)
	set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${SAMPJIT_VERSION_STRING}-linux")
	set(CPACK_GENERATOR TGZ)
endif()

include(CPack)
